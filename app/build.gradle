import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'findbugs'
apply plugin: 'kotlin-android-extensions'

ext {
  git = Grgit.open()
  gitVersionCode = git.log(includes: ['HEAD']).size()
}

android {
  compileSdkVersion build_versions.target_sdk
  buildToolsVersion build_versions.build_tools
  defaultConfig {
    applicationId "com.leodeleon.popmovies"
    minSdkVersion build_versions.min_sdk
    targetSdkVersion build_versions.target_sdk
    versionCode gitVersionCode
    versionName build_versions.version_number
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  signingConfigs {
    release
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    buildTypes.each {
      it.buildConfigField 'String', 'MOVIE_DB_API_KEY', MovieDBApiKey
      it.buildConfigField 'String', 'GOOGLE_APIS_KEY', DeveloperConsoleApiKey
    }
  }
  configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
  }
  testOptions {
    unitTests.returnDefaultValues = true
  }
  sourceSets {
    main {
      java{
        srcDirs += 'src/main/kotlin'
      }
    }
  }
}

dependencies {

  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation files('libs/YouTubeAndroidPlayerApi.jar')

  //TODO: REMOVE THOSE
  implementation 'net.opacapp:multiline-collapsingtoolbar:1.3.1'

  implementation deps.kotlin.stdlib
  implementation deps.kotlin.koinArch
  implementation deps.kotlin.ktx
  implementation deps.support.appcompat
  implementation deps.support.v4
  implementation deps.support.constraint
  implementation deps.support.design
  implementation deps.support.cardview
  implementation deps.support.customTabs
  implementation deps.support.lifecycle
  implementation deps.support.lifecycleExtensions
  implementation deps.support.room
  implementation deps.support.paging
  implementation deps.support.pagingRx
  implementation deps.support.roomRx
  implementation deps.support.navigation
  implementation deps.support.navigationFragment
  implementation deps.google.firebaseCrash
  implementation deps.net.moshi
  implementation deps.net.retrofit
  implementation deps.net.retrofitMoshi
  implementation deps.net.retrofitRxJava
  implementation deps.net.okhttp
  implementation deps.net.okhttpLogging
  implementation deps.net.glide
  implementation deps.rx.rxjava
  implementation deps.rx.rxandroid
  implementation deps.rx.rxkotlin
  implementation deps.rx.rxbinding
  implementation deps.rx.rxbindingSupport
  implementation deps.rx.rxbindingDesign
  implementation deps.rx.rxbindingAppCompat
  implementation deps.rx.rxbindingRecycler
  implementation deps.debug.timber
  implementation deps.debug.stetho
  debugImplementation deps.debug.chuck
  releaseImplementation deps.debug.chuckNoOp

  kapt deps.kapt.lifecycle
  kapt deps.kapt.room

  testImplementation deps.kotlin.stdlib
  testImplementation deps.test.mockito
  testImplementation deps.test.junit
  androidTestImplementation deps.test.mockitoAndroid
  androidTestImplementation (deps.test.espresso) {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
}

apply plugin: 'com.google.gms.google-services'
apply from: "${rootDir}/config/codequality-findbugs.gradle"
apply from: "${rootDir}/config/codequality-pmd.gradle"
repositories {
  mavenCentral()
}
